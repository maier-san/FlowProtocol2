~SetInputDescription Baue ein Expertensystem aus in sich verschachtelten Fragen auf und erweiter es kontinuerlich.
~Set Skriptpfad=D:\Entwicklung\MSanGitDev\FlowProtocol2\Scripts\Beispiele\Expertensystemtool.fp2
~Set NFTabs=
~Set NFRefLine=5
// ***************************************************
// ***************************************************
~Execute
~GoSub NeueFrage; BaseKey=W
~End
~DefineSub NeueFrage
	~If $SkipNeueFrage!=Ja
		?QNeu: Gibt es ausgehend von den jetzt schon gegebenen Antworten eine weitere Frage?
			#j: Ja
				~Input FNeu: Wie lautet die Frage für die Stelle?
					&> Formuliere die Frage so, dass eine Auswahl von Antworten vorgegeben werden kann.			
				~Set T=$Chr(9)
				~Replace EE=$NFTabs|T->$Chr(9)
				~Random RKey=1000..9999
				~Set CKey=E$RKey
				@Skript-Erweiterung >>* Erweitere das Skript um die Frage "$FNeu".
					> Öffne die Skriptdatei im Texteditor
						>.|$Skriptpfad
					> und füge nach Zeile $NFRefLine folgenden Codeblock ein:
					>.|$EE~Set NFTabs=$NFTabsT
					>.|$EE?$CKey: $FNeu
					>.|$EE$T// Oberhalb von hier neue Einträge einfügen
					>.|$EE$T#x: Nicht in Liste
					>.|$EE$T$T~Set NATabs=$NFTabsT
					>.|$EE$T$T~Set NARefLine=$Chr(36)LineNumber-3
					>.|$EE$T$T~Set NAFrage=$Chr(36)$CKey_OptionGroupPrompt
					>.|$EE$T$T~GoSub NeueAntwort; BaseKey=$BaseKeyF
				@Skript-Erweiterung >>* Setze die Ausführung fort.
					> Speichere das Skript mit der Änderung.
					> Aktualisiere das Browserfenster.
			#n: Nein, im Moment nicht
~Return
~DefineSub NeueAntwort
	~Input ANeu: Wie lautet die neue Antwortmöglichkeit auf die Frage "$NAFrage"?
	~CamelCase CKey=$ANeu
	~Set T=$Chr(9)
	~Replace EE=$NATabs|T->$Chr(9)
	@Skript-Erweiterung >>* Erweitere das Skript um die neue Antwortmöglichkeit "$ANeu" auf die Frage "$NAFrage".
		> Öffne die Skriptdatei im Texteditor
			>.|$Skriptpfad
		> und füge vor Zeile $NARefLine folgenden Codeblock ein:				
		@Skript-Erweiterung >.|$EE#$CKey: $ANeu
		~GoSub ErfasseSituation; BaseKey=$BaseKeyS
		>.|$EE$T~Set NFRefLine=$Chr(36)LineNumber-1
	~Set SkipNeueFrage=Ja
~Return
~DefineSub ErfasseSituation
	~Set SitPromt=aufgrund der oben eingegebenen Antwort auf die Frage "$NAFrage"
	~If ?$ANeu!=
		~Set SitPromt=aufgrund der Antwort "$ANeu" auf die Frage "$NAFrage"
	~Input SitZF: Fasse die Situation $SitPromt kurz zusammen.
	@Zusammenfassung >> $SitZF
	@Skript-Erweiterung >.|$EE$T@Zusammenfassung >> $SitZF
	?SitL: Soll ein Lösungsansatz für diese Situation formuliert werden?
		&> Gemeint ist die Situation $SitPromt
		#j: Ja
			~Input SitLA: Beschreibe den Lösungsansatz
				&> Für die Situation aufgrund der Antwort "$ANeu" auf die Frage "$NAFrage".
			@Lösungsansatz >> $SitLA
			@Skript-Erweiterung >.|$EE$T@Lösungsansatz >> $SitLA
		#n: Nein
~Return